plugins {
    id 'org.springframework.boot' version '2.3.0.RELEASE'
    id "com.github.node-gradle.node" version "2.2.4"
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

archivesBaseName = 'spring-boot-kisses-angular'
version = '0.4.0'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-web")
}

def generatedResources = "src/generated/resources"

sourceSets {
    main {
        output.dir(generatedResources, builtBy: 'webpack')
    }
}

node {
    version = '12.17.0'
    npmVersion = '6.14.5'
    download = true
}

task webpack(type: NodeTask, dependsOn: 'npmInstall') {
    script = project.file('node_modules/webpack/bin/webpack.js')
    args = ['--display-error-details']
}

task serve(type: NodeTask, dependsOn: 'webpack') {
    script = project.file('node_modules/webpack-dev-server/bin/webpack-dev-server.js')
    args = ['--content-base', generatedResources,
            '--host', '0.0.0.0']
}

task audit(type: NpmTask, dependsOn: 'npmInstall') {
    args = ['audit']
}

processResources.dependsOn 'webpack'

clean.delete << file('node_modules')
clean.delete << file(generatedResources)
