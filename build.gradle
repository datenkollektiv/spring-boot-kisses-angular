plugins {
    id 'org.springframework.boot' version '1.5.3.RELEASE'
    id "com.moowork.node" version "1.1.1"
}

apply plugin: 'org.springframework.boot'
apply plugin: 'com.moowork.node'

jar {
    baseName = 'spring-boot-kisses-angular'
    version =  '0.2.0-SNAPSHOT'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
}

node {
    version = '7.9.0'
    npmVersion = '4.2.0'
    download = true
}

task webpack(type: NodeTask, dependsOn: 'npmInstall') {
    script = project.file('node_modules/.bin/webpack')
    args = ['--display-error-details']
}

task serve(type: NodeTask, dependsOn: 'webpack') {
    script = project.file('node_modules/.bin/webpack-dev-server')
    args = ['--content-base', 'src/main/resources/app',
            '--host', '0.0.0.0']
}

processResources.dependsOn 'webpack'

clean.delete << file('node_modules')

task wrapper(type: Wrapper) {
    gradleVersion = '4.0.2'
}
