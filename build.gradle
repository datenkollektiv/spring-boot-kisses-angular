plugins {
    id 'org.springframework.boot' version '2.0.0.RELEASE'
    id "com.moowork.node" version "1.2.0"
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.moowork.node'

archivesBaseName = 'spring-boot-kisses-angular'
version = '0.4.0.BUILD-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
}

def generatedResources = "src/generated/resources"

sourceSets {
    main {
        output.dir(generatedResources, builtBy: 'webpack')
    }
}

node {
    version = '8.9.4'
    npmVersion = '5.6.0'
    download = true
}

task webpack(type: NodeTask, dependsOn: 'npmInstall') {
    script = project.file('node_modules/webpack/bin/webpack.js')
    args = ['--display-error-details']
}

task serve(type: NodeTask, dependsOn: 'webpack') {
    script = project.file('node_modules/webpack-dev-server/bin/webpack-dev-server.js')
    args = ['--content-base', generatedResources,
            '--host', '0.0.0.0']
}

processResources.dependsOn 'webpack'

clean.delete << file('node_modules')
clean.delete << file(generatedResources)
